use crate::nana_ast::*;

grammar;

pub Nana: Nana = {
    <BlockInner> => <>.into(),
};

Expr: Expr = {
    <AppExpr>,
    <ControlFlow> => <>.into(),
};

AppExpr: Expr = {
    <AtomExpr> => <>.into(),
    <Application> => <>.into(),
};

/// A semantically minimal expr, 
/// meaning that AtomExpr must be associated within itself.
///
/// Note that `()` wrapped expr is now in Struct(Tuple).
AtomExpr: Expr = {
    <Block> => <>.into(),
    <Binder> => <>.into(),
    <Literal> => <>.into(),
};

ControlFlow: ControlFlow = {
    "?" <e:AppExpr> <bs:MatchingBranch+> => (e, bs).into(),
};

MatchingBranch: (Pattern, Expr) = {
    "|" <p:Pattern> "->" <e:AppExpr> => (p, e),
};

Application: Application = {
    <func:AppExpr> <arg:AtomExpr> => (func,arg).into(),
};

Block: Block = {
    "[" <BlockInner> "]" => (Sturcture::Sum, <>).into(),
    "(" <BlockInner> ")" => (Sturcture::Product, <>).into(),
    "{" <BlockInner> "}" => (Sturcture::Labeled, <>).into(),
};

BlockInner: (Vec<Binding>, Vec<Molecule>) = {
    <bs:Binding*> <ms:Comma<Molecule>> => (bs, ms),
};

Molecule: Molecule = {
    <Expr> => <>.into()
};

Literal: Literal = {
    <INTCONST> => <>.into(),
    <FLTCONST> => <>.into(),
    <STRCONST> => <>.into(),
    <RAWCONST> => Literal::Raw(<>.into()),
};

Binding: Binding = {
    <hs:Head+> <e:Expr> ";" => (hs,e).into(),
};

Binder: Binder = {
    <IDENT> => <>.into(),
    <UIDENT> => <>.into(),
    <SYMBOLIDENT> => <>.into(),
};

// Note: Head is now guarded by "~" to reduce ambiguity.
Head: Head = {
    "~" <b:Binder> <ps:Pattern+> <m:Mask> => (b,ps,m).into(), // Fixme: Pattern+
    "~" <p:Pattern> <m:Mask> => (p,m).into(),
};

Mask: Mask = {
    "=" => Mask::Closed,
    ":=" => Mask::Exposed,
};

Pattern: Pattern = {
    "(" <AliasPattern> ")" => <>,
    <Binder> => Pattern::Binder(<>),
    <WILDCARD> => Pattern::Wild,
    <FORTHEREST> => Pattern::Rest,
    "<" <SemiColonOnce<ExposurePattern>> ">" => Pattern::Exposure(<>),
    "[" <ps:Comma<Pattern>> "]" => Pattern::Vector(ps),
    "(" <ps:Comma<Pattern>> ")" => Pattern::Tuple(ps),
    "{" <ps:Comma<AppExpr>> "}" => Pattern::HashMap(ps),
};

ExposurePattern: ExposurePattern = {
    <Binder> => ExposurePattern::Binder(<>),
    <FORTHEREST> => ExposurePattern::All,
};

AliasPattern: Pattern = {
    <al:Pattern> "=" <p:Pattern> => (al, p).into(),
}

// Identifiers

IDENT: String = r"[a-zA-Z][_0-9a-zA-Z]*'*" => <>.into();
UIDENT: String = r"_[a-zA-Z][_0-9a-zA-Z]*'*" => <>.into();
WILDCARD: String = "_" => <>.into();
FORTHEREST: String = ".." => <>.into();
SYMBOLIDENT: String = "(" <syn:r"[~!@#$%^&/\?\|\+\*]"> ")" => syn.into(); // Todo: [+-*/~!$%&]


// Const

INTCONST: u64 = {
    r"[0-9]+" => <>.parse::<u64>().unwrap(),
    r"0[xX][0-9a-fA-F]+" => {
        let s = <>
            .trim_start_matches("0x")
            .trim_start_matches("0X");
        u64::from_str_radix(s, 16).unwrap()
    },
};
FLTCONST: f64 = {
    r"[0-9]*\.[0-9]+([eE][-\+]?[0-9]+)?" => <>.parse::<f64>().unwrap(),
    r"[0-9]+[eE][-\+]?[0-9]+" => <>.parse::<f64>().unwrap(),
};
STRCONST: String = r#""([^"\\]|\\.)*""# => <>.into();
RAWCONST: String = r"\[\|.*\|\]" => <>.into();

// Utility

#[inline]
Comma<T>: Vec<T> = {
    <mut v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
};

#[inline]
CommaOnce<T>: Vec<T> = {
    <mut v:(<T> ",")*> <e:T> => {
        v.push(e);
        v
    }
};

#[inline]
SemiColon<T>: Vec<T> = {
    <mut v:(<T> ";")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
};

#[inline]
SemiColonOnce<T>: Vec<T> = {
    <mut v:(<T> ";")*> <e:T> => {
        v.push(e);
        v
    }
};
